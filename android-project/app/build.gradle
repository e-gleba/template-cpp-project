def buildAsLibrary = project.hasProperty('BUILD_AS_LIBRARY')
def buildAsApplication = !buildAsLibrary
if (buildAsApplication) {
    apply plugin: 'com.android.application'
}
else {
    apply plugin: 'com.android.library'
}

android {
    compileSdk 35
    ndkVersion '27.0.12077973'

    defaultConfig {
        if (buildAsApplication) {
            applicationId "mil.eugene.gamedev"
        }
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }
    buildTypes {
        debug {
            debuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    applicationVariants.configureEach { variant ->
        tasks["merge${variant.name.capitalize()}Assets"]
            .dependsOn("externalNativeBuild${variant.name.capitalize()}")
    }
    if (!project.hasProperty('EXCLUDE_NATIVE_LIBS')) {
        sourceSets.main {
            jniLibs.srcDir 'libs'
        }
        externalNativeBuild {
            cmake {
                version("4.0.2")
                path '../../CMakeLists.txt'
            }
        }
       
    }
    namespace 'mil.eugene.gamedev'

    if (buildAsLibrary) {
        libraryVariants.all { variant ->
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith(".aar")) {
                    def fileName = "org.libsdl.app.aar"
                    output.outputFile = new File(outputFile.parent, fileName)
                }
            }
        }
    }

    sourceSets {
        main {
            assets {
                srcDirs = ['../../assets']
            }
        }
    }
    lint {
        abortOnError false
    }
    buildToolsVersion '36.0.0'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
}
